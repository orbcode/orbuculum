# SPDX-License-Identifier: GPL-2.0-or-later

#
# STMicroelectronics ST-LINK/V1, ST-LINK/V2, ST-LINK/V2-1, STLINK-V3 in-circuit
# debugger/programmer
#

adapter driver st-link
st-link vid_pid 0x0483 0x3744 0x0483 0x3748 0x0483 0x374b 0x0483 0x374d 0x0483 0x374e 0x0483 0x374f 0x0483 0x3752 0x0483 0x3753 0x0483 0x3754 0x0483 0x3755 0x0483 0x3757

# script for stm32h7x family

#
# stm32h7 devices support both JTAG and SWD transports.
#
source [find target/swj-dp.tcl]
source [find mem_helper.tcl]

set DUAL_BANK 1


if { [info exists CHIPNAME] } {
   set _CHIPNAME $CHIPNAME
} else {
   set _CHIPNAME stm32h7x
}

if { [info exists DUAL_BANK] } {
	set $_CHIPNAME.DUAL_BANK $DUAL_BANK
	unset DUAL_BANK
} else {
	set $_CHIPNAME.DUAL_BANK 0
}

if { [info exists DUAL_CORE] } {
	set $_CHIPNAME.DUAL_CORE $DUAL_CORE
	unset DUAL_CORE
} else {
	set $_CHIPNAME.DUAL_CORE 0
}

# Issue a warning when hla is used, and fallback to single core configuration
if { [set $_CHIPNAME.DUAL_CORE] && [using_hla] } {
	echo "Warning : hla does not support multicore debugging"
	set $_CHIPNAME.DUAL_CORE 0
}

if { [info exists USE_CTI] } {
	set $_CHIPNAME.USE_CTI $USE_CTI
	unset USE_CTI
} else {
	set $_CHIPNAME.USE_CTI 0
}

# Issue a warning when DUAL_CORE=0 and USE_CTI=1, and fallback to USE_CTI=0
if { ![set $_CHIPNAME.DUAL_CORE] && [set $_CHIPNAME.USE_CTI] } {
	echo "Warning : could not use CTI with a single core device, CTI is disabled"
	set $_CHIPNAME.USE_CTI 0
}

set _ENDIAN little

# Work-area is a space in RAM used for flash programming
# By default use 64kB
if { [info exists WORKAREASIZE] } {
   set _WORKAREASIZE $WORKAREASIZE
} else {
   set _WORKAREASIZE 0x10000
}

#jtag scan chain
if { [info exists CPUTAPID] } {
   set _CPUTAPID $CPUTAPID
} else {
   if { [using_jtag] } {
	  set _CPUTAPID 0x6ba00477
   } {
      set _CPUTAPID 0x6ba02477
   }
}

swj_newdap $_CHIPNAME cpu -irlen 4 -ircapture 0x1 -irmask 0xf -expected-id $_CPUTAPID
dap create $_CHIPNAME.dap -chain-position $_CHIPNAME.cpu

if {[using_jtag]} {
   jtag newtap $_CHIPNAME bs -irlen 5
}

if {![using_hla]} {
	# STM32H7 provides an APB-AP at access port 2, which allows the access to
	# the debug and trace features on the system APB System Debug Bus (APB-D).
	target create $_CHIPNAME.ap2 mem_ap -dap $_CHIPNAME.dap -ap-num 2
	swo  create $_CHIPNAME.swo  -dap $_CHIPNAME.dap -ap-num 2 -baseaddr 0xE00E3000
	tpiu create $_CHIPNAME.tpiu -dap $_CHIPNAME.dap -ap-num 2 -baseaddr 0xE00F5000
}

target create $_CHIPNAME.cpu0 cortex_m -endian $_ENDIAN -dap $_CHIPNAME.dap -ap-num 0

$_CHIPNAME.cpu0 configure -work-area-phys 0x20000000 -work-area-size $_WORKAREASIZE -work-area-backup 0

flash bank $_CHIPNAME.bank1.cpu0 stm32h7x 0x08000000 0 0 0 $_CHIPNAME.cpu0

if {[set $_CHIPNAME.DUAL_BANK]} {
	flash bank $_CHIPNAME.bank2.cpu0 stm32h7x 0x08100000 0 0 0 $_CHIPNAME.cpu0
}

if {[set $_CHIPNAME.DUAL_CORE]} {
	target create $_CHIPNAME.cpu1 cortex_m -endian $_ENDIAN -dap $_CHIPNAME.dap -ap-num 3

	$_CHIPNAME.cpu1 configure -work-area-phys 0x38000000 -work-area-size $_WORKAREASIZE -work-area-backup 0

	flash bank $_CHIPNAME.bank1.cpu1 stm32h7x 0x08000000 0 0 0 $_CHIPNAME.cpu1

	if {[set $_CHIPNAME.DUAL_BANK]} {
		flash bank $_CHIPNAME.bank2.cpu1 stm32h7x 0x08100000 0 0 0 $_CHIPNAME.cpu1
	}
}

# Make sure that cpu0 is selected
targets $_CHIPNAME.cpu0

if { [info exists QUADSPI] && $QUADSPI } {
   set a [llength [flash list]]
   set _QSPINAME $_CHIPNAME.qspi
   flash bank $_QSPINAME stmqspi 0x90000000 0 0 0 $_CHIPNAME.cpu0 0x52005000
} else {
   if { [info exists OCTOSPI1] && $OCTOSPI1 } {
      set a [llength [flash list]]
      set _OCTOSPINAME1 $_CHIPNAME.octospi1
      flash bank $_OCTOSPINAME1 stmqspi 0x90000000 0 0 0 $_CHIPNAME.cpu0 0x52005000
   }
   if { [info exists OCTOSPI2] && $OCTOSPI2 } {
      set b [llength [flash list]]
      set _OCTOSPINAME2 $_CHIPNAME.octospi2
      flash bank $_OCTOSPINAME2 stmqspi 0x70000000 0 0 0 $_CHIPNAME.cpu0 0x5200A000
   }
}

# Clock after reset is HSI at 64 MHz, no need of PLL
adapter speed 4000

adapter srst delay 100
if {[using_jtag]} {
 jtag_ntrst_delay 100
}

# use hardware reset
#
# The STM32H7 does not support connect_assert_srst mode because the AXI is
# unavailable while SRST is asserted, and that is used to access the DBGMCU
# component at 0x5C001000 in the examine-end event handler.
#
# It is possible to access the DBGMCU component at 0xE00E1000 via AP2 instead
# of the default AP0, and that works with SRST asserted; however, nonzero AP
# usage does not work with HLA, so is not done by default. That change could be
# made in a local configuration file if connect_assert_srst mode is needed for
# a specific application and a non-HLA adapter is in use.
reset_config srst_nogate

if {![using_hla]} {
   # if srst is not fitted use SYSRESETREQ to
   # perform a soft reset
	$_CHIPNAME.cpu0 cortex_m reset_config sysresetreq

	if {[set $_CHIPNAME.DUAL_CORE]} {
		$_CHIPNAME.cpu1 cortex_m reset_config sysresetreq
	}

   # Set CSW[27], which according to ARM ADI v5 appendix E1.4 maps to AHB signal
   # HPROT[3], which according to AMBA AHB/ASB/APB specification chapter 3.7.3
   # makes the data access cacheable. This allows reading and writing data in the
   # CPU cache from the debugger, which is far more useful than going straight to
   # RAM when operating on typical variables, and is generally no worse when
   # operating on special memory locations.
   $_CHIPNAME.dap apcsw 0x08000000 0x08000000
}

$_CHIPNAME.cpu0 configure -event examine-end {
	# Enable D3 and D1 DBG clocks
	# DBGMCU_CR |= D3DBGCKEN | D1DBGCKEN
	stm32h7x_dbgmcu_mmw 0x004 0x00600000 0

	# Enable debug during low power modes (uses more power)
	# DBGMCU_CR |= DBG_STANDBY | DBG_STOP | DBG_SLEEP D1 Domain
	stm32h7x_dbgmcu_mmw 0x004 0x00000007 0
	# DBGMCU_CR |= DBG_STANDBY | DBG_STOP | DBG_SLEEP D2 Domain
	stm32h7x_dbgmcu_mmw 0x004 0x00000038 0

	# Stop watchdog counters during halt
	# DBGMCU_APB3FZ1 |= WWDG1
	stm32h7x_dbgmcu_mmw 0x034 0x00000040 0
	# DBGMCU_APB1LFZ1 |= WWDG2
	stm32h7x_dbgmcu_mmw 0x03C 0x00000800 0
	# DBGMCU_APB4FZ1 |= WDGLSD1 | WDGLSD2
	stm32h7x_dbgmcu_mmw 0x054 0x000C0000 0

	# Enable clock for tracing
	# DBGMCU_CR |= TRACECLKEN
	stm32h7x_dbgmcu_mmw 0x004 0x00100000 0

	# RM0399 (id 0x450) M7+M4 with SWO Funnel
	# RM0433 (id 0x450) M7 with SWO Funnel
	# RM0455 (id 0x480) M7 without SWO Funnel
	# RM0468 (id 0x483) M7 without SWO Funnel
	# Enable CM7 and CM4 slave ports in SWO trace Funnel
	# Works ok also on devices single core and without SWO funnel
	# Hack, use stm32h7x_dbgmcu_mmw with big offset to control SWTF
	# SWTF_CTRL |= ENS0 | ENS1
	stm32h7x_dbgmcu_mmw 0x3000 0x00000003 0

	# Configure ITM with SYNC packets enabled
	mww 0xE0000E80 0x0001000F   ;# TCR: enable ITM with TraceBusID=1, SYNCENA=1, TSENA=1
	mww 0xE0000E00 0x00000001   ;# TER: enable ITM channel 0

	# Configure and enable SWO (NRZ/UART protocol)
	set _CHIPNAME [stm32h7x_get_chipname]
}

$_CHIPNAME.cpu0 configure -event reset-init {
	# Clock after reset is HSI at 64 MHz, no need of PLL
	adapter speed 4000
}

# get _CHIPNAME from current target
proc stm32h7x_get_chipname {} {
	set t [target current]
	set sep [string last "." $t]
	if {$sep == -1} {
		return $t
	}
	return [string range $t 0 [expr {$sep - 1}]]
}

if {[set $_CHIPNAME.DUAL_CORE]} {
	$_CHIPNAME.cpu1 configure -event examine-end {
		set _CHIPNAME [stm32h7x_get_chipname]
		global $_CHIPNAME.USE_CTI

		# Stop watchdog counters during halt
		# DBGMCU_APB3FZ2 |= WWDG1
		stm32h7x_dbgmcu_mmw 0x038 0x00000040 0
		# DBGMCU_APB1LFZ2 |= WWDG2
		stm32h7x_dbgmcu_mmw 0x040 0x00000800 0
		# DBGMCU_APB4FZ2 |= WDGLSD1 | WDGLSD2
		stm32h7x_dbgmcu_mmw 0x058 0x000C0000 0

		if {[set $_CHIPNAME.USE_CTI]} {
			stm32h7x_cti_start
		}
	}
}

# like mrw, but with target selection
proc stm32h7x_mrw {used_target reg} {
	return [$used_target read_memory $reg 32 1]
}

# like mmw, but with target selection
proc stm32h7x_mmw {used_target reg setbits clearbits} {
	set old [stm32h7x_mrw $used_target $reg]
	set new [expr {($old & ~$clearbits) | $setbits}]
	$used_target mww $reg $new
}

# mmw for dbgmcu component registers, it accepts the register offset from dbgmcu base
# this procedure will use the mem_ap on AP2 whenever possible
proc stm32h7x_dbgmcu_mmw {reg_offset setbits clearbits} {
	# use $_CHIPNAME.ap2 if possible, and use the proper dbgmcu base address
	if {![using_hla]} {
		set _CHIPNAME [stm32h7x_get_chipname]
		set used_target $_CHIPNAME.ap2
		set reg_addr [expr {0xE00E1000 + $reg_offset}]
	} {
		set used_target [target current]
		set reg_addr [expr {0x5C001000 + $reg_offset}]
	}

	stm32h7x_mmw $used_target $reg_addr $setbits $clearbits
}

if {[set $_CHIPNAME.USE_CTI]} {
	# create CTI instances for both cores
	cti create $_CHIPNAME.cti0 -dap $_CHIPNAME.dap -ap-num 0 -baseaddr 0xE0043000
	cti create $_CHIPNAME.cti1 -dap $_CHIPNAME.dap -ap-num 3 -baseaddr 0xE0043000

	$_CHIPNAME.cpu0 configure -event halted { stm32h7x_cti_prepare_restart_all }
	$_CHIPNAME.cpu1 configure -event halted { stm32h7x_cti_prepare_restart_all }

	$_CHIPNAME.cpu0 configure -event debug-halted { stm32h7x_cti_prepare_restart_all }
	$_CHIPNAME.cpu1 configure -event debug-halted { stm32h7x_cti_prepare_restart_all }

	proc stm32h7x_cti_start {} {
		set _CHIPNAME [stm32h7x_get_chipname]

		# Configure Cores' CTIs to halt each other
		# TRIGIN0 (DBGTRIGGER) and TRIGOUT0 (EDBGRQ) at CTM_CHANNEL_0
		$_CHIPNAME.cti0 write INEN0 0x1
		$_CHIPNAME.cti0 write OUTEN0 0x1
		$_CHIPNAME.cti1 write INEN0 0x1
		$_CHIPNAME.cti1 write OUTEN0 0x1

		# enable CTIs
		$_CHIPNAME.cti0 enable on
		$_CHIPNAME.cti1 enable on
	}

	proc stm32h7x_cti_stop {} {
		set _CHIPNAME [stm32h7x_get_chipname]

		$_CHIPNAME.cti0 enable off
		$_CHIPNAME.cti1 enable off
	}

	proc stm32h7x_cti_prepare_restart_all {} {
		stm32h7x_cti_prepare_restart cti0
		stm32h7x_cti_prepare_restart cti1
	}

	proc stm32h7x_cti_prepare_restart {cti} {
		set _CHIPNAME [stm32h7x_get_chipname]

		# Acknowlodge EDBGRQ at TRIGOUT0
		$_CHIPNAME.$cti write INACK 0x01
		$_CHIPNAME.$cti write INACK 0x00
	}
}
$_CHIPNAME.cpu0 configure -rtos auto

set cfg_dir [file dirname [info script]]
source [file join $cfg_dir debug_init.tcl]
# -----------------------------
# SWV/TPIU/ITM configuration
# -----------------------------
# Configuration moved to examine-end event to ensure SWO object exists
$_CHIPNAME.swo configure -protocol uart -traceclk 480000000 -pin-freq 2000000 -formatter on -output :46000
$_CHIPNAME.swo enable
# ==============================================================================
# Helper Functions (for telnet from orbtop)
# ==============================================================================

# --- Only used constants ---
set CORESIGHT_LAR_KEY 0xC5ACCE55      ;# CoreSight Lock Access Register key

# ITM Registers
set ITM_TCR     0xE0000E80            ;# ITM Trace Control Register
set ITM_LAR     0xE0000FB0            ;# ITM Lock Access Register
set ITM_TCR_DWTENA      0x00000008    ;# Bit 3: DWT stimulus Enable
set ITM_TCR_TSENA       0x00000002    ;# Bit 1: Timestamp Enable
set ITM_TCR_SYNCENA     0x00000004    ;# Bit 2: Sync packet Enable

# DWT Registers
set DWT_CTRL    0xE0001000            ;# DWT Control Register
set DWT_CYCCNT  0xE0001004            ;# DWT Cycle Count Register
set DWT_LAR     0xE0001FB0            ;# DWT Lock Access Register
set DWT_COMP1   0xE0001030            ;# DWT Comparator 1 Address
set DWT_MASK1   0xE0001034            ;# DWT Comparator 1 Mask
set DWT_FUNC1   0xE0001038            ;# DWT Comparator 1 Function

# DWT_CTRL bit definitions
set DWT_CTRL_CYCCNTENA  0x00000001    ;# Bit 0: Cycle counter enable
set DWT_CTRL_POSTPRESET 0x000001E0    ;# Bits 8:5: POSTCNT preset value
set DWT_CTRL_POSTINIT   0x00001E00    ;# Bits 12:9: POSTCNT initial value
set DWT_CTRL_CYCTAP     0x00000200    ;# Bit 9: Cycle counter tap for POSTCNT
set DWT_CTRL_SYNCTAP_MASK 0x00000C00  ;# Bits 11:10: Sync packet tap select
set DWT_CTRL_SYNCTAP_DISABLED 0x00000000  ;# 00: Sync disabled
set DWT_CTRL_SYNCTAP_BIT24   0x00000400  ;# 01: Sync when CYCCNT[24] toggles
set DWT_CTRL_SYNCTAP_BIT26   0x00000800  ;# 10: Sync when CYCCNT[26] toggles
set DWT_CTRL_SYNCTAP_BIT28   0x00000C00  ;# 11: Sync when CYCCNT[28] toggles
set DWT_CTRL_PCSAMPLENA 0x00001000    ;# Bit 12: PC sampling enable
set DWT_CTRL_EXCTRCENA  0x00010000    ;# Bit 16: Exception trace enable
set DWT_CTRL_CPIEVTENA  0x00020000    ;# Bit 17: CPI event enable
set DWT_CTRL_EXCEVTENA  0x00040000    ;# Bit 18: Exception event enable
set DWT_CTRL_SLEEPEVTENA 0x00080000   ;# Bit 19: Sleep event enable
set DWT_CTRL_LSUEVTENA  0x00100000    ;# Bit 20: LSU event enable
set DWT_CTRL_FOLDEVTENA 0x00200000    ;# Bit 21: Fold event enable

# DWT comparator function settings
set DWT_FUNC_DATA_VALUE 0x0000000D    ;# Data value compare on write
set DWT_MASK_DISABLED   0x00000000    ;# No masking - exact address match

# DEMCR Register
set DEMCR       0xE000EDFC            ;# Debug Exception and Monitor Control Register
set DEMCR_TRCENA        0x01000000    ;# Bit 24: Trace enable

proc rtos_dwt_config {address} {
    global DEMCR DEMCR_TRCENA
    global ITM_LAR DWT_LAR CORESIGHT_LAR_KEY
    global DWT_CTRL DWT_CTRL_CYCCNTENA DWT_CTRL_SYNCTAP_BIT24
    global DWT_COMP1 DWT_MASK1 DWT_FUNC1 DWT_FUNC_DATA_VALUE
    global ITM_TCR ITM_TCR_DWTENA ITM_TCR_TSENA ITM_TCR_SYNCENA
    global DWT_CYCCNT
    global DWT_MASK_DISABLED

    echo "Configuring DWT Comparator 1 for address 0x[format %08X $address]"

    # Enable trace in DEMCR
    mmw $DEMCR $DEMCR_TRCENA 0

    # Unlock ITM and DWT
    mww $ITM_LAR $CORESIGHT_LAR_KEY
    mww $DWT_LAR $CORESIGHT_LAR_KEY

    # Reset and enable cycle counter for ITM timestamps
    mww $DWT_CYCCNT 0

    # Enable CYCCNT and set SYNCTAP to generate SYNC packets at CYCCNT[24]
    mmw $DWT_CTRL [expr {$DWT_CTRL_CYCCNTENA | $DWT_CTRL_SYNCTAP_BIT24}] 0

    # Ensure DWT events are enabled in ITM_TCR with SYNC packets
    mmw $ITM_TCR [expr {$ITM_TCR_DWTENA | $ITM_TCR_TSENA | $ITM_TCR_SYNCENA}] 0

    # Configure DWT Comparator 1 for data write tracking
    mww $DWT_COMP1 $address
    mww $DWT_MASK1 $DWT_MASK_DISABLED
    mww $DWT_FUNC1 $DWT_FUNC_DATA_VALUE

    echo "DWT Comparator 1 configured for data write at 0x[format %08X $address]"
    echo "DWT Cycle counter enabled for timestamps"
    echo "SYNC packets enabled at CYCCNT 24Hz @ 480MHz"
}

# Configurable PC sampling - frequency in Hz
proc pc_sampling_config {freq_hz} {
    global DWT_CTRL DWT_CTRL_PCSAMPLENA DWT_CTRL_CYCCNTENA
    global DWT_CTRL_SYNCTAP_MASK DWT_CTRL_SYNCTAP_DISABLED
    global DWT_CTRL_SYNCTAP_BIT24 DWT_CTRL_SYNCTAP_BIT26 DWT_CTRL_SYNCTAP_BIT28

    # Calculate SYNCTAP value based on frequency
    # SYNCTAP controls when to generate sync packets based on CYCCNT bit toggle
    # At 480MHz: bit24=~24Hz, bit26=~6Hz, bit28=~1.5Hz

    if {$freq_hz == 0} {
        echo "PC sampling disabled"
        mmw $DWT_CTRL 0 $DWT_CTRL_PCSAMPLENA
        return
    }

    set tap_val 0
    if {$freq_hz > 10} {
        set tap_val $DWT_CTRL_SYNCTAP_BIT24  ;# Tap at CYCCNT[24] ~24Hz
        echo "PC sampling: 24Hz (tap at CYCCNT bit 24)"
    } elseif {$freq_hz > 3} {
        set tap_val $DWT_CTRL_SYNCTAP_BIT26  ;# Tap at CYCCNT[26] ~6Hz
        echo "PC sampling: 6Hz (tap at CYCCNT bit 26)"
    } else {
        set tap_val $DWT_CTRL_SYNCTAP_BIT28  ;# Tap at CYCCNT[28] ~1.5Hz
        echo "PC sampling: 1.5Hz (tap at CYCCNT bit 28)"
    }

    # Clear old SYNCTAP bits and set new ones
    mmw $DWT_CTRL 0 $DWT_CTRL_SYNCTAP_MASK
    mmw $DWT_CTRL [expr {$DWT_CTRL_PCSAMPLENA | $DWT_CTRL_CYCCNTENA | $tap_val}] 0
    echo "WARNING: PC sampling impacts performance"
}

# Legacy functions for compatibility
proc pc_sampling_enable {} {
    pc_sampling_config 24  ;# Default to highest rate
}

proc pc_sampling_disable {} {
    pc_sampling_config 0
}

proc exception_trace_enable {} {
    global DEMCR DEMCR_TRCENA
    global DWT_CTRL DWT_CTRL_EXCTRCENA

    echo "DWT: enabling exception trace"

    # Asegura que el tracing estÃ© activo
    mmw $DEMCR $DEMCR_TRCENA 0

    # Activa Exception tracing y el cycle counter
    mmw $DWT_CTRL [expr {$DWT_CTRL_EXCTRCENA}] 0
}

# Disable exception trace
proc exception_trace_disable {} {
    global DWT_CTRL DWT_CTRL_EXCTRCENA

    echo "DWT: disabling exception trace"

    mmw $DWT_CTRL 0 $DWT_CTRL_EXCTRCENA
}

# Control SYNC packet generation with configurable rate
proc sync_config {rate} {
    global ITM_TCR ITM_TCR_SYNCENA
    global DWT_CTRL DWT_CTRL_SYNCTAP_MASK
    global DWT_CTRL_SYNCTAP_DISABLED DWT_CTRL_SYNCTAP_BIT24
    global DWT_CTRL_SYNCTAP_BIT26 DWT_CTRL_SYNCTAP_BIT28

    if {$rate == 0} {
        # Disable SYNC in ITM
        mmw $ITM_TCR 0 $ITM_TCR_SYNCENA
        # Clear SYNCTAP in DWT
        mmw $DWT_CTRL 0 $DWT_CTRL_SYNCTAP_MASK
        echo "SYNC packets disabled"
        return
    }

    # Enable SYNC in ITM
    mmw $ITM_TCR $ITM_TCR_SYNCENA 0

    # Configure SYNCTAP rate in DWT
    mmw $DWT_CTRL 0 $DWT_CTRL_SYNCTAP_MASK  ;# Clear old value

    if {$rate >= 24} {
        mmw $DWT_CTRL $DWT_CTRL_SYNCTAP_BIT24 0
        echo "SYNC packets enabled at 24Hz CYCCNT bit 24"
    } elseif {$rate >= 6} {
        mmw $DWT_CTRL $DWT_CTRL_SYNCTAP_BIT26 0
        echo "SYNC packets enabled at 6Hz CYCCNT bit 26"
    } else {
        mmw $DWT_CTRL $DWT_CTRL_SYNCTAP_BIT28 0
        echo "SYNC packets enabled at 1.5Hz CYCCNT bit 28"
    }
}

# Legacy functions for compatibility
proc sync_enable {} {
    sync_config 24  ;# Default to 24Hz
}

proc sync_disable {} {
    sync_config 0
}

echo "==================================================================================="
echo "DWT config via telnet: rtos_dwt_config <addr> to use with Orbtop -T"
echo "pc_sampling_config <freq>: Configure PC sampling (0=off, 1=1.5Hz, 6=6Hz, 24=24Hz)"
echo "sync_enable/disable: Control SYNC packet generation"
echo "exception_trace_enable: Enable exception tracing"
echo "==================================================================================="